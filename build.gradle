buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "com.ofg:micro-common-release:0.1.18"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6"
        classpath "com.netflix.nebula:gradle-extra-configurations-plugin:3.0.3"
    }
    dependencies {
        ant.unjar src: configurations.classpath.find { it.name.startsWith("micro-common-release") }, dest: 'build/release'
    }
}

apply from: "${rootProject.buildDir}/release/gradle/version.gradle"

scmVersion {
    tag {
        prefix = 'release'
        versionSeparator = '/'
    }
}

group = "io.fourfinanceit.pipeline"
version = scmVersion.version

apply plugin: 'groovy'
apply from: "${rootProject.buildDir}/release/gradle/publish.gradle"
apply from: "${rootProject.buildDir}/release/gradle/release.gradle"

bintray {
    pkg {
        repo = 'jenkins-pipeline'
        vcsUrl = 'https://github.com/4finance/jenkins-pipeline-dsl.git'
        labels = ['jenkins', 'pipeline', 'ci', 'cd', 'continuous-integration', 'continuous-delivery']
    }
}

apply plugin: 'nebula.provided-base'

repositories {
    maven { url 'http://repo.jenkins-ci.org/releases/' }
    jcenter()
    mavenCentral()
}

ext {
    jenkinsVersion = '1.609.2'
    jobDslVersion = '1.44'
}

dependencies {
    provided 'org.codehaus.groovy:groovy-all:1.8.9'
    provided 'com.google.guava:guava:14.0'
    provided("org.jenkins-ci.plugins:job-dsl-core:${project.ext.jobDslVersion}") {
        exclude module: 'groovy-all'
    }

    testCompile('org.spockframework:spock-core:0.7-groovy-1.8') {
        exclude module: 'groovy-all'
    }
}

bintrayUpload.dependsOn 'build'