plugins {
    id 'com.jfrog.bintray' version '1.6'
    id 'nebula.optional-base' version '3.0.3'
    id 'pl.allegro.tech.build.axion-release' version '1.3.4'
}

apply plugin: 'groovy'
apply plugin: 'nebula.provided-base'
apply plugin: 'maven-publish'

scmVersion {
    ignoreUncommittedChanges = false

    tag {
        prefix = ''
    }
}

repositories {
    maven { url 'http://repo.jenkins-ci.org/releases/' }
    jcenter()
    mavenCentral()
}

ext {
    jenkinsVersion = '1.609.2'
    jobDslVersion = '1.44'
}

group = "io.fourfinanceit.pipeline"
version = scmVersion.version

dependencies {
    provided 'org.codehaus.groovy:groovy-all:1.8.9'
    provided 'com.google.guava:guava:14.0'
    provided("org.jenkins-ci.plugins:job-dsl-core:${project.ext.jobDslVersion}") {
        exclude module: 'groovy-all'
    }

    testCompile('org.spockframework:spock-core:0.7-groovy-1.8') {
        exclude module: 'groovy-all'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'jenkins-pipeline'
        name = project.name
        userOrg = '4finance'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/4finance/jenkins-pipeline-dsl.git'
    }
}

bintrayUpload.dependsOn 'build'